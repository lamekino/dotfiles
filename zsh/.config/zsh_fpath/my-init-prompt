# vim:ft=zsh

export PROMPT_COLOR_ERROR_LEVEL=1
export PROMPT_COLOR_JOB_COUNT=3
export PROMPT_COLOR_PY_VENV=14
export PROMPT_COLOR_DIR_STACK=140
export PROMPT_COLOR_GIT_CLEAN=10
export PROMPT_COLOR_GIT_UNSTAGED=168
export PROMPT_COLOR_PWD=140
export PROMPT_COLOR_TIP=15

function my-prompt-config {
    local fields=(\
        PROMPT_COLOR_ERROR_LEVEL \
        PROMPT_COLOR_JOB_COUNT \
        PROMPT_COLOR_PY_VENV \
        PROMPT_COLOR_DIR_STACK \
        PROMPT_COLOR_GIT_CLEAN \
        PROMPT_COLOR_GIT_UNSTAGED \
        PROMPT_COLOR_PWD \
        PROMPT_COLOR_TIP \
    )

    for f in ${fields[@]}; do
        echo "$f=${(P)f}"
    done
}

function prompt-color {
    local color=$1 text="$2"

    printf "%%F{%s}%s%%f" "${color}" "${text}"
}

function git-prompt-msg {
    local name="$1"
    local num_changes=$(git status --short 2>/dev/null | wc -l)

    if [ $num_changes -eq 0 ]; then
        prompt-color "$PROMPT_COLOR_GIT_CLEAN" "@${name}"
    else
        prompt-color "$PROMPT_COLOR_GIT_UNSTAGED" "@${name}+${num_changes}"
    fi

    printf ":"
}

function build-prompt {
    function prompt-git-status { : }
    function prompt-py-venv { : }
    function prompt-dir-count { : }
    function prompt-vi-mode { : }

    local numdirs=$(dirs -v | wc -l)
    local git_branch=$(git branch --show-current 2>/dev/null)

    local errlevel="$(prompt-color "$PROMPT_COLOR_ERROR_LEVEL" "%?")"
    local jobcount="$(prompt-color "$PROMPT_COLOR_JOB_COUNT" '%%%j')"
    local dir="$(prompt-color "$PROMPT_COLOR_PWD" '%~')"

    if [ -n "$VIRTUAL_ENV"  ]; then
        function prompt-py-venv {
            prompt-color "$PROMPT_COLOR_PY_VENV" '(venv) '
        }
    fi

    if [ -n "$git_branch" ]; then
        function prompt-git-status {
            git-prompt-msg "$git_branch"
        }
    fi

    if [ $numdirs -gt 1 ]; then
        function prompt-dir-count {
            prompt-color "$PROMPT_COLOR_DIR_STACK" "~$numdirs"
        }
    fi

    case $KEYMAP in
    viins|main)
        function prompt-vi-mode {
            prompt-color "$PROMPT_COLOR_TIP" '%#'
        }
        ;;
    vicmd)
        function prompt-vi-mode {
            prompt-color "$PROMPT_COLOR_TIP" ':'
        }
        ;;
    esac

    printf "%s%s%s%s%s%s%s " \
        "$(prompt-py-venv)" \
        "$(prompt-dir-count)" \
        "%(?..${errlevel} )" \
        "%(1j.${jobcount} .)" \
        "$(prompt-git-status)" \
        "$dir" \
        "$(prompt-vi-mode)"
}

# this function runs for each prompt redraw and for each keypress
function zle-line-init zle-keymap-select {
    PROMPT="$(build-prompt)"
    zle reset-prompt
}

zle -N zle-line-init
zle -N zle-keymap-select
